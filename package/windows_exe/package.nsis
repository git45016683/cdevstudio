; ===== Defines =====
!define APPLICATION_NAME "cdevstudio"
!define APPLICATION_VERSION "0.0.6"
!define PROJECT_ROOT "..\.."
;!define QT_BINARY_DIR "C:\Qt\Qt5.1.0\5.1.0\msvc2012\bin"
;!define QT_PLUGINS_DIR "C:\Qt\Qt5.1.0\5.1.0\msvc2012\plugins"

!ifndef QT_BINARY_DIR
	!error "QT_BINARY_DIR is not defined"
!endif

!ifndef QT_PLUGINS_DIR
	!error "QT_PLUGINS_DIR is not defined"
!endif

; ===== Includes =====
!include "MUI2.nsh"

; ===== General =====
Name "${APPLICATION_NAME} ${APPLICATION_VERSION}"
OutFile "${APPLICATION_NAME}-${APPLICATION_VERSION}-installer.exe"
InstallDir "$PROGRAMFILES\${APPLICATION_NAME}"
SetCompressor "lzma"

; ===== Pages =====
!insertmacro MUI_PAGE_WELCOME
!insertmacro MUI_PAGE_LICENSE "..\..\LICENSE.txt"
!insertmacro MUI_PAGE_DIRECTORY
!insertmacro MUI_PAGE_INSTFILES
!define MUI_FINISHPAGE_NOAUTOCLOSE
!define MUI_FINISHPAGE_RUN
!define MUI_FINISHPAGE_RUN_CHECKED
!define MUI_FINISHPAGE_RUN_TEXT "Run Application"
!define MUI_FINISHPAGE_RUN_FUNCTION "RunApplication"
!insertmacro MUI_PAGE_FINISH

!insertmacro MUI_UNPAGE_WELCOME
!insertmacro MUI_UNPAGE_CONFIRM
!insertmacro MUI_UNPAGE_INSTFILES
!insertmacro MUI_UNPAGE_FINISH

!insertmacro MUI_LANGUAGE "English"

; ===== Installer =====
Section ""
	SetOutPath $INSTDIR
	
	CreateDirectory $INSTDIR\platforms
	
	!ifdef MINGW-MAKE
		File ${PROJECT_ROOT}\build\src\cdevstudio\cdevstudio.exe
		File ${PROJECT_ROOT}\build\src\cdevstudiocodeedit\libcdevstudiocodeedit.dll
		File ${PROJECT_ROOT}\build\src\cdevstudioplatform\libcdevstudiosystemplatform.dll
		File ${PROJECT_ROOT}\build\src\cdevstudioplatform\libcdevstudioprojectplatform.dll
		File ${PROJECT_ROOT}\build\src\cdevstudiobackend\libcdevstudiobackend.dll
		File ${QT_BINARY_DIR}\libgcc_s_dw2-1.dll
		File ${QT_BINARY_DIR}\libstdc++-6.dll
		File ${QT_BINARY_DIR}\libwinpthread-1.dll
	!else ifdef MSVC-MSBUILD
		File ${PROJECT_ROOT}\build\src\cdevstudio\Release\cdevstudio.exe
		File ${PROJECT_ROOT}\build\src\cdevstudiocodeedit\Release\cdevstudiocodeedit.dll
		File ${PROJECT_ROOT}\build\src\cdevstudioplatform\Release\cdevstudiosystemplatform.dll
		File ${PROJECT_ROOT}\build\src\cdevstudioplatform\Release\cdevstudioprojectplatform.dll
		File ${PROJECT_ROOT}\build\src\cdevstudiobackend\Release\cdevstudiobackend.dll
	!else
		!error "The compiler platform is not defined. Please run the batch file for your compiler platform"
	!endif
	
	File ${PROJECT_ROOT}\LICENSE.txt
	
	File ${QT_BINARY_DIR}\Qt5Widgets.dll
	File ${QT_BINARY_DIR}\Qt5Gui.dll
	File ${QT_BINARY_DIR}\Qt5Core.dll
	File ${QT_BINARY_DIR}\icudt51.dll
	File ${QT_BINARY_DIR}\icuin51.dll
	File ${QT_BINARY_DIR}\icuuc51.dll
	
	SetOutPath $INSTDIR\platforms
	File ${QT_PLUGINS_DIR}\platforms\qminimal.dll
	File ${QT_PLUGINS_DIR}\platforms\qwindows.dll
	
	SetOutPath $INSTDIR
	WriteUninstaller $INSTDIR\uninstall.exe
SectionEnd

; ===== Uninstaller =====
Section "Uninstall"
	Delete $INSTDIR\*.exe
	Delete $INSTDIR\*.dll
	Delete $INSTDIR\platforms\*.dll
	Delete $INSTDIR\LICENSE.txt
	Delete $INSTDIR\uninstall.exe
	RMDir $INSTDIR\platforms
	RMDir $INSTDIR
SectionEnd

; ===== Functions =====
Function RunApplication
	ExecShell "" "$INSTDIR\cdevstudio.exe"
FunctionEnd
